apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-kapt'

apply plugin: "androidx.navigation.safeargs"

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "in.bitotsav"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName "1.0.1"
        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "${variant.name}-${variant.versionName}.apk"
                }
                variant.assembleProvider.get().doLast {
                    if (variant.mappingFile != null && variant.mappingFile.exists()) {
                        def mappingFilename = "$archivesBaseName-$variant.baseName-mapping-${variant.versionName}.txt"
                        (new File(variant.mappingFile.parent, mappingFilename)).delete()
                        variant.mappingFile.renameTo(variant.mappingFile.parent +
                                "/" + mappingFilename)
                    }
                }
            }
        }
    }
    dataBinding {
        enabled = true
    }
}

ext {
    // TODO: Move these to project.ext in project level build.gradle
    gson_version = "2.8.5"
    room_version = "2.1.0-alpha04"
    coroutines_version = "1.1.0"
    firebase_core_version = "16.0.7"
    firebase_messaging_version = "17.3.4"
    firebase_jobdispatcher_version = "0.8.5"
    retrofit_version = "2.5.0"
    retrofit2_kotlin_coroutines_version = "0.9.2"
    nav_version = "1.0.0-beta01"
    koin_version = "2.0.0-beta-1"
    workmanager_version = "1.0.0-beta05"
    lifecycle_version = "2.1.0-alpha02"
    material_version = "1.0.0"
    safetynet_version = "16.0.0"
    appkillermanager_version = "2.1.1"
    smartapprate_version = "1.0.8"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0-alpha02'
    implementation 'androidx.core:core-ktx:1.1.0-alpha04'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.2-alpha01'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.2-alpha01'

    // ViewModel & LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // Room
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-coroutines:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Gson
    implementation "com.google.code.gson:gson:$gson_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Firebase
    implementation "com.google.firebase:firebase-core:$firebase_core_version"
    implementation "com.google.firebase:firebase-messaging:$firebase_messaging_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$retrofit2_kotlin_coroutines_version"

    // Navigation
    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"

    // Koin
    implementation "org.koin:koin-android:$koin_version"
    implementation "org.koin:koin-androidx-viewmodel:$koin_version"
    testImplementation "org.koin:koin-test:$koin_version"

    // WorkManager
    implementation "android.arch.work:work-runtime-ktx:$workmanager_version"

    // Material Theme
    implementation "com.google.android.material:material:$material_version"

    // Captcha
    implementation "com.google.android.gms:play-services-safetynet:$safetynet_version"

    // 3rd party
//    https://github.com/thelittlefireman/AppKillerManager/tree/master
    implementation "com.thelittlefireman:AppKillerManager:$appkillermanager_version"

//    https://github.com/codemybrainsout/smart-app-rate
    implementation "com.codemybrainsout.rating:ratingdialog:$smartapprate_version"


}

apply plugin: 'com.google.gms.google-services'
